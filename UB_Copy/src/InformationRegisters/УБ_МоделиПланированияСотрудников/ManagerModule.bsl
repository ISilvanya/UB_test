#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ТаблицаМоделиПланированияСотрудников() Экспорт
	
	ТипыКадровыхДокументов = Новый Массив;
	ТипыКадровыхДокументов.Добавить(Тип("ДокументСсылка.КадровыйПеревод"));
	ТипыКадровыхДокументов.Добавить(Тип("ДокументСсылка.КадровыйПереводСписком"));
	ТипыКадровыхДокументов.Добавить(Тип("ДокументСсылка.ПриемНаРаботу"));
	ТипыКадровыхДокументов.Добавить(Тип("ДокументСсылка.ПриемНаРаботуСписком"));
	ТипыКадровыхДокументов.Добавить(Тип("ДокументСсылка.Увольнение"));
	
	МоделиПланированияСотрудников = Новый ТаблицаЗначений;
	МоделиПланированияСотрудников.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	МоделиПланированияСотрудников.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	МоделиПланированияСотрудников.Колонки.Добавить("КадровыйДокумент", Новый ОписаниеТипов(ТипыКадровыхДокументов));
	МоделиПланированияСотрудников.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	МоделиПланированияСотрудников.Колонки.Добавить("МодельПланированияЭффективности", Новый ОписаниеТипов("СправочникСсылка.УБ_МоделиПланированияЭффективности"));
	МоделиПланированияСотрудников.Колонки.Добавить("Грейд", Новый ОписаниеТипов("СправочникСсылка.УБ_Грейды"));
	
	Возврат МоделиПланированияСотрудников;
	
КонецФункции

Процедура ЗаписатьМодельПланированияСотрудника(ТаблицаМоделиСотрудников, Отказ) Экспорт
	
	Если ТаблицаМоделиСотрудников.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КадровыеДокументы = ТаблицаМоделиСотрудников.ВыгрузитьКолонку("КадровыйДокумент");
	КадровыеДокументы = ОбщегоНазначенияКлиентСервер.СвернутьМассив(КадровыеДокументы);
	
	Для Каждого КадровыйДокумент Из КадровыеДокументы Цикл
		
		НаборЗаписей = РегистрыСведений.УБ_МоделиПланированияСотрудников.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.КадровыйДокумент.Установить(КадровыйДокумент);
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПодсистемы") Тогда
			Если ТипЗнч(КадровыйДокумент) = Тип("ДокументСсылка.КадровыйПеревод")
				Или ТипЗнч(КадровыйДокумент) = Тип("ДокументСсылка.КадровыйПереводСписком") Тогда
				ВидСобытия = "Кадровый перевод";
			ИначеЕсли ТипЗнч(КадровыйДокумент) = Тип("ДокументСсылка.ПриемНаРаботу")
				Или ТипЗнч(КадровыйДокумент) = Тип("ДокументСсылка.ПриемНаРаботуСписком") Тогда
				ВидСобытия = "Прием на работу";
			КонецЕсли;
		Иначе
			ВидСобытия = "";
		КонецЕсли;
		
		СтрокиМоделиСотрудников = ТаблицаМоделиСотрудников.НайтиСтроки(Новый Структура("КадровыйДокумент", КадровыйДокумент));
		Для Каждого СтрокаМодели Из СтрокиМоделиСотрудников Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаМодели);
			НоваяЗапись.ВидСобытия = ВидСобытия;
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МоделиСотрудников.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ВТ_КадровыеДокументыСотрудников
		|ИЗ
		|	&ТаблицаМоделиСотрудников КАК МоделиСотрудников
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеМоделейСотрудников.Период КАК Период,
		|	ДанныеМоделейСотрудников.Сотрудник КАК Сотрудник,
		|	ДанныеМоделейСотрудников.Сотрудник.Представление КАК СотрудникПредставление,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеМоделейСотрудников.КадровыйДокумент) КАК КадровыйДокумент
		|ИЗ
		|	РегистрСведений.УБ_МоделиПланированияСотрудников.СрезПоследних(
		|			,
		|			Сотрудник В
		|				(ВЫБРАТЬ
		|					КадровыеДокументыСотрудников.Сотрудник КАК Сотрудник
		|				ИЗ
		|					ВТ_КадровыеДокументыСотрудников КАК КадровыеДокументыСотрудников)) КАК ДанныеМоделейСотрудников
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеМоделейСотрудников.Период,
		|	ДанныеМоделейСотрудников.Сотрудник,
		|	ДанныеМоделейСотрудников.Сотрудник.Представление
		|
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеМоделейСотрудников.КадровыйДокумент) > 1";
	
	Запрос.УстановитьПараметр("ТаблицаМоделиСотрудников", ТаблицаМоделиСотрудников);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сотруднику ""%1"" на дату %2 уже назначена модель планирования'"),
			Выборка.СотрудникПредставление,
			Формат(Выборка.Период, "ДЛФ=Д"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли