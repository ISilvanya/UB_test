#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	КонтрольДублированияЦелей(Отказ);
	
КонецПроцедуры                  


#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
Процедура КонтрольДублированияЦелей(Отказ)        
	
	Регистратор = ЭтотОбъект.Отбор.Регистратор.Значение;        
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Регистратор, "Проведен, Утвержден");
	Если Реквизиты.Проведен и Реквизиты.Утвержден Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
	    "ВЫБРАТЬ
	    |	УБ_ПланПоПоказателям.Показатель КАК Показатель,
	    |	УБ_ПланПоПоказателям.Назначение КАК Назначение,
	    |	УБ_ПланПоПоказателям.ДатаНачала КАК ДатаНачала,
	    |	УБ_ПланПоПоказателям.ДатаОкончания КАК ДатаОкончания
	    |ПОМЕСТИТЬ втДок
	    |ИЗ
	    |	РегистрСведений.УБ_ПланПоПоказателям КАК УБ_ПланПоПоказателям
	    |ГДЕ
	    |	УБ_ПланПоПоказателям.Регистратор = &Регистратор
	    |
	    |ИНДЕКСИРОВАТЬ ПО
	    |	Показатель,
	    |	Назначение
	    |;
	    |
	    |////////////////////////////////////////////////////////////////////////////////
	    |ВЫБРАТЬ РАЗЛИЧНЫЕ
	    |	втДок.Показатель КАК Показатель,
	    |	втДок.Назначение КАК Назначение,
	    |	УБ_ПланПоПоказателям.Регистратор КАК Регистратор
	    |ПОМЕСТИТЬ втРезультат
	    |ИЗ
	    |	втДок КАК втДок
	    |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УБ_ПланПоПоказателям КАК УБ_ПланПоПоказателям
	    |		ПО (втДок.Показатель = УБ_ПланПоПоказателям.Показатель)
		|	И (втДок.Назначение = УБ_ПланПоПоказателям.Назначение)
		|	И ((втДок.ДатаНачала >= УБ_ПланПоПоказателям.ДатаНачала
		|		И втДок.ДатаОкончания <= УБ_ПланПоПоказателям.ДатаОкончания)
		|	ИЛИ (втДок.ДатаНачала <= УБ_ПланПоПоказателям.ДатаНачала
		|	    И втДок.ДатаОкончания >= УБ_ПланПоПоказателям.ДатаОкончания))
		|	//ИЛИ втДок.ДатаОкончания <= УБ_ПланПоПоказателям.ДатаОкончания)
		|		И Не УБ_ПланПоПоказателям.Регистратор = &Регистратор	    
	    |СГРУППИРОВАТЬ ПО
	    |	втДок.Показатель,
	    |	втДок.Назначение,
	    |	УБ_ПланПоПоказателям.Регистратор
	    |;
	    |
	    |////////////////////////////////////////////////////////////////////////////////
	    |ВЫБРАТЬ
	    |	втРезультат.Показатель КАК Показатель,
	    |	втРезультат.Назначение КАК Назначение,
	    |	втРезультат.Регистратор КАК Регистратор
	    |ИЗ
	    |	втРезультат КАК втРезультат
	    |ГДЕ
	    |	НЕ втРезультат.Регистратор = ЗНАЧЕНИЕ(Документ.УБ_ПланированиеЦелейИПоказателей.ПустаяСсылка)";
		
		Запрос.УстановитьПараметр("Регистратор", ЭтотОбъект.Отбор.Регистратор.Значение);        
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон("Показатель %1 уже запланирован для назначения %2 документом %3 ",
											Выборка.Показатель,
											Выборка.Назначение,
											Выборка.Регистратор); 
				Сообщение.КлючДанных = Выборка.регистратор;	
				Сообщение.УстановитьДанные(Выборка.Регистратор);
				Сообщение.Сообщить();
				Отказ = Истина;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.
						|en = 'Invalid object call on the client.''");
#КонецЕсли	