#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ЗаписатьДелегированныхСотрудниковПодразделения(Таблица, Движения, Отказ) Экспорт
	
	Если Отказ Или Таблица.Количество() = 0 Тогда	
		Возврат;
	КонецЕсли; 

	ДвиженияДелегированныеСотрудникиПодразделения = Движения.УБ_ДелегированныеСотрудникиПодразделения;
	ДвиженияДелегированныеСотрудникиПодразделения.Записывать = Истина;
	ДвиженияДелегированныеСотрудникиПодразделения.Загрузить(Таблица); 	
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтение
	|ГДЕ
	|	ЗначениеРазрешено(Подразделение)";

КонецПроцедуры

#КонецОбласти

#КонецОбласти   

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.УБ_ДелегированныеСотрудникиПодразделения.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "1.0.2.4";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Комментарий = НСтр("ru = 'Заполнение в существующих записях  регистра ""Делегированные сотрудники подразделений"" добавленного измерения ""Вид операции"" значением по умолчанию'");
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	 //Выберем всех регистраторов регистра
    Запрос = Новый Запрос;
    Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
        | УБ_ДелегированныеСотрудникиПодразделения.Регистратор
        |ИЗ
        | РегистрСведений.УБ_ДелегированныеСотрудникиПодразделения КАК УБ_ДелегированныеСотрудникиПодразделения";
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
	        НаборЗаписей = РегистрыСведений.УБ_ДелегированныеСотрудникиПодразделения.СоздатьНаборЗаписей(); 
	        НаборЗаписей.Отбор.Регистратор.Значение = Выборка.Регистратор;
	        НаборЗаписей.Прочитать();
	        Для каждого Запись Из НаборЗаписей Цикл
	            Запись.ВидОперации = Перечисления.УБ_ВидыОперацийДелегирования.ПодсистемаПланирования;
	        КонецЦикла;
	        НаборЗаписей.Записать();
				
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось заполнить запись регистра ""Делегирование сотрудников подразделения "" ""%1"" по причине: %2'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстСообщения,
				Выборка.Запись,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Обновление информационной базы'"),
				УровеньЖурналаРегистрации.Предупреждение,
				Выборка.Регистратор.Метаданные(),
				Выборка.Регистратор,
				ТекстСообщения);
			
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли