//Источник ТабличныеЧастиУНФ
#Область ПрограммныйИнтерфейс


// Добавляет ключ связи в табличную часть.
//
// Параметры:
//  ФормаДокумента	 - ФормаКлиентскогоПриложения	 - Форма документа, реквизиты которой обрабатываются процедурой
//  Строка			 - ДанныеФормыСтруктура			 - Данные текущей строки таблицы формы
//
Процедура ДобавитьКлючСвязиВСтрокуТабличнойЧасти(ФормаДокумента, Строка = Неопределено, ИмяРеквизитаСвязи = "КлючСвязи") Экспорт
	
	Если Строка <> Неопределено Тогда
		СтрокаТабличнойЧасти = ФормаДокумента.Объект.Показатели.НайтиПоИдентификатору(Строка);
	Иначе
		СтрокаТабличнойЧасти = ФормаДокумента.Элементы[ФормаДокумента.ИмяТабличнойЧасти].ТекущиеДанные;
	КонецЕсли;
	
	СтрокаТабличнойЧасти[ИмяРеквизитаСвязи]= УБ_ТабличныеЧастиКлиентСервер.СоздатьНовыйКлючСвязи(ФормаДокумента, ИмяРеквизитаСвязи);
	
КонецПроцедуры

// Добавляет ключ связи в подчиненную табличную часть.
//
// Параметры:
//  ФормаДокумента				 - ФормаКлиентскогоПриложения	 - Форма документа, реквизиты которой обрабатываются процедурой
//	ИмяПодчиненнойТабличнойЧасти - Строка						 - Имя подчиненной табличной части
//  Строка						 - ДанныеФормыСтруктура			 - Данные текущей строки таблицы формы
//
Процедура ДобавитьКлючСвязиВСтрокуПодчиненнойТабличнойЧасти(ФормаДокумента, ИмяПодчиненнойТабличнойЧасти, Строка = Неопределено, ИмяКлюча = "КлючСвязи") Экспорт
	
	ПодчиненнаяТабличнаяЧасть = ФормаДокумента.Элементы[ИмяПодчиненнойТабличнойЧасти];
	
	Если Строка = Неопределено Тогда
		СтрокаПодчиненнойТабличнойЧасти = ПодчиненнаяТабличнаяЧасть.ТекущиеДанные;
	Иначе
		СтрокаПодчиненнойТабличнойЧасти = Строка;
	КонецЕсли;
	СтрокаПодчиненнойТабличнойЧасти[ИмяКлюча] = ПодчиненнаяТабличнаяЧасть.ОтборСтрок[ИмяКлюча];
	
	СтрОтбора = Новый ФиксированнаяСтруктура(ИмяКлюча, ПодчиненнаяТабличнаяЧасть.ОтборСтрок[ИмяКлюча]);
	ФормаДокумента.Элементы[ИмяПодчиненнойТабличнойЧасти].ОтборСтрок = СтрОтбора;

КонецПроцедуры

// Проверка выбора основной строки перед операциями с подчиненной ТЧ.
//
// Параметры:
//  ФормаДокумента				 - ФормаКлиентскогоПриложения	 - Форма документа, реквизиты которой обрабатываются процедурой
//	ИмяПодчиненнойТабличнойЧасти - Строка						 - Имя подчиненной табличной части
//
// Возвращаемое значение:
//  Булево - Истина, если строка не выбрана
//
Функция НеВыбранаСтрокаОсновнойТЧ(ФормаДокумента, ИмяПодчиненнойТабличнойЧасти) Экспорт

	Если ФормаДокумента.Элементы[ФормаДокумента.ИмяТабличнойЧасти].ТекущиеДанные = Неопределено Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не выбрана строка основной табличной части.'");
		Сообщение.Сообщить();
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
		
КонецФункции

// Удаляет строки из подчиненной табличной части.
//
// Параметры:
//  ФормаДокумента				 - ФормаКлиентскогоПриложения	 - Форма документа, реквизиты которой обрабатываются процедурой
//	ИмяПодчиненнойТабличнойЧасти - Строка						 - Имя подчиненной табличной части
//
Процедура УдалитьСтрокиПодчиненнойТабличнойЧасти(ФормаДокумента, ИмяПодчиненнойТабличнойЧасти, ИмяРеквизитаСвязи = "КлючСвязи") Экспорт
	
	СтрокаТабличнойЧасти = ФормаДокумента.Элементы[ФормаДокумента.ИмяТабличнойЧасти].ТекущиеДанные;
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПодчиненнаяТабличнаяЧасть = ФормаДокумента.Объект[ИмяПодчиненнойТабличнойЧасти];
	
	РезультатПоиска = ПодчиненнаяТабличнаяЧасть.НайтиСтроки(Новый Структура(ИмяРеквизитаСвязи, СтрокаТабличнойЧасти[ИмяРеквизитаСвязи]));
	Для каждого СтрокаПоиска Из  РезультатПоиска Цикл
		ИндексУдаления = ПодчиненнаяТабличнаяЧасть.Индекс(СтрокаПоиска);
		ПодчиненнаяТабличнаяЧасть.Удалить(ИндексУдаления);
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает отбор на подчиненную табличную часть.
//
// Параметры:
//  ФормаДокумента				 - ФормаКлиентскогоПриложения	 - Форма документа, реквизиты которой обрабатываются процедурой
//	ИмяПодчиненнойТабличнойЧасти - Строка						 - Имя подчиненной табличной части
//  Строка						 - ДанныеФормыСтруктура			 - Данные текущей строки таблицы формы
//
Процедура УстановитьОтборНаПодчиненнуюТабличнуюЧасть(ФормаДокумента, ИмяПодчиненнойТабличнойЧасти, Строка = Неопределено, ИмяРеквизитаСвязи = "КлючСвязи") Экспорт
	
	Если Строка <> Неопределено Тогда
		СтрокаТабличнойЧасти = Строка;
	Иначе
		СтрокаТабличнойЧасти = ФормаДокумента.Элементы[ФормаДокумента.ИмяТабличнойЧасти].ТекущиеДанные;
	КонецЕсли;
	
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		СтрОтбора = Новый ФиксированнаяСтруктура(ИмяРеквизитаСвязи, Неопределено);	
	Иначе
		СтрОтбора = Новый ФиксированнаяСтруктура(ИмяРеквизитаСвязи, СтрокаТабличнойЧасти[ИмяРеквизитаСвязи]);	
	КонецЕсли; 
	
	ФормаДокумента.Элементы[ИмяПодчиненнойТабличнойЧасти].ОтборСтрок = СтрОтбора;
	
КонецПроцедуры

#КонецОбласти


 