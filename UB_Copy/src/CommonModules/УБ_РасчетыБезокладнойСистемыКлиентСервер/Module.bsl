 
Функция РаспределитьСуммуПропорциональноКоэффициентамСПредельнымКоэффициентом(Адрес) Экспорт
	
	РассчитанныеДанные = ПолучитьИзВременногоХранилища(Адрес);
	
	РаспределитьСуммуПропорциональноКоэффициентомСПредельнымКоэффициентомВспомогательный(РассчитанныеДанные);	
	
	АдресИтог = ПоместитьВоВременноеХранилище(РассчитанныеДанные);
	
	Возврат АдресИтог;
	
КонецФункции

Процедура РаспределитьСуммуПропорциональноКоэффициентомСПредельнымКоэффициентомВспомогательный(СтруктураДляРасчета)
	
	Коэффициенты = Новый Массив;
	Для Каждого СтрокаСотрудника Из СтруктураДляРасчета.СписокДляРаспределения Цикл
		Коэффициенты.Добавить(СтрокаСотрудника.КоэффициентРаспределения);
	КонецЦикла;
	
	Результат = РаспределитьСуммуПропорциональноКоэффициентам(?(СтруктураДляРасчета.СуммаОстаток = 0,СтруктураДляРасчета.СуммаРаспределения,СтруктураДляРасчета.СуммаОстаток),Коэффициенты);
	
	Если Результат <> Неопределено Тогда
		Для Индекс = 0 По Результат.Количество() - 1 Цикл
			СтрокаСотрудника = СтруктураДляРасчета.СписокДляРаспределения[Индекс];
			СтрокаСотрудника.Сумма = Результат[Индекс];
		КонецЦикла;
	КонецЕсли;
	
	МассивРассчитаных = Новый Массив;
	СуммаРассчитаных = 0;
	Для каждого Строка из СтруктураДляРасчета.СписокДляРаспределения Цикл
		Если Строка.ПредельныйКоэффициентРЭ*СтруктураДляРасчета.СуммаРаспределения < Строка.Сумма Тогда
			Строка.Сумма = Строка.ПредельныйКоэффициентРЭ*СтруктураДляРасчета.СуммаРаспределения;
			СуммаРассчитаных = СуммаРассчитаных+Строка.Сумма;
		    МассивРассчитаных.Добавить(Строка);
		КонецЕсли;
	КонецЦикла;
	
	СтруктураДляРасчета.СуммаОстаток = ?(СтруктураДляРасчета.СуммаОстаток = 0,СтруктураДляРасчета.СуммаРаспределения,СтруктураДляРасчета.СуммаОстаток) - СуммаРассчитаных;
	
	Для каждого Строка из МассивРассчитаных Цикл
		Отбор = Новый Структура;
		Отбор.Вставить("Сотрудник", Строка.Сотрудник);
		Найденный = СтруктураДляРасчета.ТаблицаРасчета.НайтиСтроки(Отбор);
		Найденный[0].Сумма = Строка.Сумма;
		//Найденный = СтруктураДляРасчета.СуммаРаспределения.НайтиСтроки(Отбор);
		СтруктураДляРасчета.СписокДляРаспределения.Удалить(СтруктураДляРасчета.СписокДляРаспределения.Индекс(Строка));
	КонецЦикла;	
	
	Если МассивРассчитаных.Количество() = 0 Тогда
		Для каждого Строка из СтруктураДляРасчета.СписокДляРаспределения Цикл 
		   Отбор = Новый Структура;
			Отбор.Вставить("Сотрудник", Строка.Сотрудник);
			Найденный = СтруктураДляРасчета.ТаблицаРасчета.НайтиСтроки(Отбор);
			Найденный[0].Сумма = Строка.Сумма;
		КонецЦикла;
		СтруктураДляРасчета.СуммаОстаток = СтруктураДляРасчета.СуммаРаспределения - СтруктураДляРасчета.ТаблицаРасчета.Итог("Сумма");
	КонецЕсли;
	
	Если СтруктураДляРасчета.СписокДляРаспределения.Количество() = 0 Тогда
		СтруктураДляРасчета.СуммаОстаток = СтруктураДляРасчета.СуммаРаспределения - СтруктураДляРасчета.ТаблицаРасчета.Итог("Сумма");	
	КонецЕсли;	
	
	Если СтруктураДляРасчета.СписокДляРаспределения.Количество()<>0 И СтруктураДляРасчета.СуммаОстаток>0 Тогда
		РаспределитьСуммуПропорциональноКоэффициентомСПредельнымКоэффициентомВспомогательный(СтруктураДляРасчета);
	КонецЕсли;	
	
		
КонецПроцедуры	

Функция РаспределитьСуммуПропорциональноКоэффициентам(Знач РаспределяемаяСумма, Знач Коэффициенты, Знач Точность = 2) Экспорт
	
	КоэффициентыАбсолютные = Новый Массив(Новый ФиксированныйМассив(Коэффициенты)); // Копируем массив в памяти.
	
	// Старое поведение при неуказанной сумме - для обратной совместимости.
	Если Не ЗначениеЗаполнено(РаспределяемаяСумма) Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	Если КоэффициентыАбсолютные.Количество() = 0 Тогда 
		// Недопустимо значение параметра Коэффициенты
		// Ожидалось: хотя бы один коэффициент будет задан.
		Возврат Неопределено;
	КонецЕсли;
	
	ИндексМаксимальногоКоэффициента = 0;
	МаксимальныйКоэффициент = 0;
	СуммаКоэффициентов = 0;
	КоэффициентыОтрицательны = (КоэффициентыАбсолютные[0] < 0);
	
	Для Индекс = 0 По КоэффициентыАбсолютные.Количество() - 1 Цикл
		Коэффициент = КоэффициентыАбсолютные[Индекс];
		
		Если КоэффициентыОтрицательны И Коэффициент > 0 Тогда 
			// Недопустимо значение параметра Коэффициенты
			// Ожидалось: все коэффициенты положительны или все отрицательны одновременно.
			Возврат Неопределено;
		КонецЕсли;
		
		Если Коэффициент < 0 Тогда 
			// Если коэффициент меньше нуля, то его абсолютное значение отрицательное ему число.
			Коэффициент = -Коэффициент; // Abs(Коэффициент)
			КоэффициентыАбсолютные[Индекс] = Коэффициент; // Заменяем коэффициент в массиве.
		КонецЕсли;
		
		Если МаксимальныйКоэффициент < Коэффициент Тогда
			МаксимальныйКоэффициент = Коэффициент;
			ИндексМаксимальногоКоэффициента = Индекс;
		КонецЕсли;
		
		СуммаКоэффициентов = СуммаКоэффициентов + Коэффициент;
	КонецЦикла;
	
	Если СуммаКоэффициентов = 0 Тогда
		// Недопустимо значение параметра Коэффициенты
		// Ожидалось: хотя бы один коэффициент будет отличен от нуля.
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Массив(КоэффициентыАбсолютные.Количество());
	
	ВыполнятьИнверсию = (РаспределяемаяСумма < 0);
	Если ВыполнятьИнверсию Тогда 
		// Если изначально распределяемая сумма меньше нуля, надо сначала распределить абсолютное значение суммы
		// затем выполнить инверсию результата.
		РаспределяемаяСумма = -РаспределяемаяСумма; // Abs(РаспределяемаяСумма).
	КонецЕсли;
	
	РаспределеннаяСумма = 0;
	
	Для Индекс = 0 По КоэффициентыАбсолютные.Количество() - 1 Цикл
		Результат[Индекс] = Окр(РаспределяемаяСумма * КоэффициентыАбсолютные[Индекс] / СуммаКоэффициентов, Точность, 1);
		РаспределеннаяСумма = РаспределеннаяСумма + Результат[Индекс];
	КонецЦикла;
	
	СуммарнаяПогрешность = РаспределяемаяСумма - РаспределеннаяСумма;
	
	Если СуммарнаяПогрешность > 0 Тогда 
		
		// Погрешности округления отнесем на коэффициент с максимальным весом.
		Если Не РаспределеннаяСумма = РаспределяемаяСумма Тогда
			Результат[ИндексМаксимальногоКоэффициента] = Результат[ИндексМаксимальногоКоэффициента] + СуммарнаяПогрешность;
		КонецЕсли;
		
	ИначеЕсли СуммарнаяПогрешность < 0 Тогда 
		
		// Если распределили больше чем положено, размазываем погрешность по ближайшим максимальным весам.
		ЗначениеПогрешности = 1 / Pow(10, Точность);
		КоличествоЭлементовПогрешности = -СуммарнаяПогрешность / ЗначениеПогрешности;
		
		Для Сч = 1 По КоличествоЭлементовПогрешности Цикл 
			МаксимальныйКоэффициент = МаксимальноеЗначениеВМассиве(КоэффициентыАбсолютные);
			Индекс = КоэффициентыАбсолютные.Найти(МаксимальныйКоэффициент);
			Результат[Индекс] = Результат[Индекс] - ЗначениеПогрешности;
			КоэффициентыАбсолютные[Индекс] = 0;
		КонецЦикла;
		
	Иначе 
		// Если СуммарнаяПогрешность = 0, то все идеально.
	КонецЕсли;
	
	Если ВыполнятьИнверсию Тогда 
		Для Индекс = 0 По КоэффициентыАбсолютные.Количество() - 1 Цикл
			Результат[Индекс] = -Результат[Индекс];
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции 

Функция МаксимальноеЗначениеВМассиве(Массив)
	
	МаксимальноеЗначение = 0;
	
	Для Индекс = 0 По Массив.Количество() - 1 Цикл
		Значение = Массив[Индекс];
		
		Если МаксимальноеЗначение < Значение Тогда
			МаксимальноеЗначение = Значение;
		КонецЕсли;
	КонецЦикла;
	
	Возврат МаксимальноеЗначение;
	
КонецФункции

Функция РассчитатьСуммуОтМаксимумаСПредельнымКоэффициентом(Адрес) Экспорт
	
	РассчитанныеДанные = ПолучитьИзВременногоХранилища(Адрес);
	
	РассчитатьСуммуОтМаксимумаСПредельнымКоэффициентомВспомогательный(РассчитанныеДанные);
	
	АдресИтог = ПоместитьВоВременноеХранилище(РассчитанныеДанные);
	
	Возврат АдресИтог;
	
КонецФункции

Процедура РассчитатьСуммуОтМаксимумаСПредельнымКоэффициентомВспомогательный(СтруктураДляРасчета)
	
	Для каждого Строка из СтруктураДляРасчета.ТаблицаРасчета Цикл
		
		БальныйКоэффициентЭффективности = ПолучитьМаксисумИРезульитатБальнойСистемы(Строка.РасчетЭффективностиСотрудника);
		МаксимальнаяСумма = СтруктураДляРасчета.СуммаРаспределения*Строка.ПредельныйКоэффициентРЭ;
		Строка.Сумма = МаксимальнаяСумма*БальныйКоэффициентЭффективности;
		
	КонецЦикла;
	
	СтруктураДляРасчета.СуммаОстаток = СтруктураДляРасчета.СуммаРаспределения - СтруктураДляРасчета.ТаблицаРасчета.Итог("Сумма");
	
КонецПроцедуры

Функция ПолучитьМаксисумИРезульитатБальнойСистемы(Документ)
	
	МаксимумБаллов = 0;
	ПолученныеБаллы = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	УБ_РасчетЭффективностиСотрудниковНачисления.ТипПоказателя КАК ТипПоказателя,
	               |	УБ_РасчетЭффективностиСотрудниковНачисления.ПоказательЭффективности КАК ПоказательЭффективности,
	               |	УБ_РасчетЭффективностиСотрудниковНачисления.Расценка КАК Расценка,
	               |	УБ_РасчетЭффективностиСотрудниковНачисления.Сумма КАК Сумма
	               |ИЗ
	               |	Документ.УБ_РасчетЭффективностиСотрудников.Начисления КАК УБ_РасчетЭффективностиСотрудниковНачисления
	               |ГДЕ
	               |	УБ_РасчетЭффективностиСотрудниковНачисления.Ссылка = &Документ
	               |	И УБ_РасчетЭффективностиСотрудниковНачисления.ТипПоказателя.ИспользоватьДляРаспределенияКоэффициентовБезокладнойСистемы = ИСТИНА
	               |	И УБ_РасчетЭффективностиСотрудниковНачисления.ПоказательЭффективности.НеУчитыватьВРасчетеПерсональнойЭффективности = ЛОЖЬ";
	Запрос.УстановитьПараметр("Документ", Документ);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		МаксимумБаллов = МаксимумБаллов+Выборка.Расценка;
		ПолученныеБаллы = ПолученныеБаллы+Выборка.Сумма;
		
	КонецЦикла;
	
	Возврат ПолученныеБаллы/МаксимумБаллов;
	
	
	
КонецФункции	