#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаМоделиПланированияСотрудников(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРуководителиПодразделений(Запрос, ТекстыЗапроса, Регистры);
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПодсистемы") Тогда
		ТекстЗапросаКадроваяИсторияСотрудников(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	УБ_ПроведениеСервер.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
КонецПроцедуры

Функция ТекстЗапросаКадроваяИсторияСотрудников(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "КадроваяИсторияСотрудников";
	
	Если Не УБ_ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДанныеДокумента.ДатаПеревода КАК Период,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ДанныеДокумента.Подразделение КАК Подразделение,
		|	ДанныеДокумента.Сотрудник КАК Сотрудник,
		|	ДанныеДокумента.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
		|	РеквизитыМодели.ШтатноеРасписание КАК Позиция,
		|	ВЫБОР
		|		КОГДА РеквизитыМодели.ШтатноеРасписание ССЫЛКА Справочник.УБ_ШтатноеРасписание
		|			ТОГДА ВЫРАЗИТЬ(РеквизитыМодели.ШтатноеРасписание КАК Справочник.УБ_ШтатноеРасписание).Должность
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК Должность,
		|	ЗНАЧЕНИЕ(Перечисление.УБ_ВидыКадровыхСобытий.Перемещение) КАК ВидСобытия
		|ИЗ
		|	Документ.УБ_ПереводНаДолжность КАК ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УБ_МоделиПланированияЭффективности КАК РеквизитыМодели
		|		ПО ДанныеДокумента.МодельПланированияЭффективности = РеквизитыМодели.Ссылка
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И ДанныеДокумента.ИзменитьПодразделениеИМодельПланирования";
	
	УБ_ОбщегоНазначения.СкорректироватьТекстЗапросаПодТекущуюКонфигурацию(ТекстЗапроса);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаМоделиПланированияСотрудников(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МоделиПланированияСотрудников";
	
	Если Не УБ_ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК КадровыйДокумент,
		|	ДанныеДокумента.ДатаПеревода КАК Период,
		|	ДанныеДокумента.Сотрудник КАК Сотрудник,
		|	ДанныеДокумента.МодельПланированияЭффективности КАК МодельПланированияЭффективности,
		|	ДанныеДокумента.Грейд КАК Грейд
		|ИЗ
		|	Документ.УБ_ПереводНаДолжность КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРуководителиПодразделений(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РуководителиПодразделений";
	
	Если Не УБ_ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДанныеДокумента.ДатаПеревода КАК Период,
		|	ДанныеДокумента.Ссылка КАК КадровыйДокумент,
		|	ДанныеДокумента.Подразделение КАК Подразделение,
		|	ДанныеДокумента.Сотрудник КАК Руководитель
		|ИЗ
		|	Документ.УБ_ПереводНаДолжность КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И ДанныеДокумента.ИзменитьПодразделениеИМодельПланирования";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Печать

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли