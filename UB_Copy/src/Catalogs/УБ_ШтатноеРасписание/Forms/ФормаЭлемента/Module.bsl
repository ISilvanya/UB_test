
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.ДатаУтверждения = НачалоМесяца(ТекущаяДата());
	КонецЕсли; 
	
	УстановитьДоступностьЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	МодельПланирования = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Объект.Ссылка).МодельПланирования(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_Модели" И Параметр.ШтатноеРасписание = Объект.Ссылка Тогда		
		МодельПланирования = Параметр.МодельПланированияЭффективности;
		УстановитьДоступностьЭлементовФормы(ЭтотОбъект);
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УтвержденаПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементовФормы(ЭтотОбъект);
	
	Если Не Объект.Утверждена Тогда
		Объект.ДатаУтверждения = '00010101';
	ИначеЕсли Объект.ДатаУтверждения = '00010101' Тогда
		Объект.ДатаУтверждения = КонецМесяца(ОбщегоНазначенияКлиент.ДатаСеанса()) + 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытаПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементовФормы(ЭтотОбъект);
	
	Если Не Объект.Закрыта Тогда
		Объект.ДатаЗакрытия = '00010101';
	ИначеЕсли Объект.ДатаЗакрытия = '00010101' Тогда
		Объект.ДатаЗакрытия = КонецМесяца(ОбщегоНазначенияКлиент.ДатаСеанса()) + 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	Объект.Наименование = ЗаполнениеНаименованияПозицииШтатногоРасписания(Объект.Подразделение, Объект.Должность);	
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьПриИзменении(Элемент)
	
	Объект.Наименование = ЗаполнениеНаименованияПозицииШтатногоРасписания(Объект.Подразделение, Объект.Должность);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Функция ЗаполнениеНаименованияПозицииШтатногоРасписания(Подразделение, Должность)
	
	НаименованиеПозиции = Строка(Должность) + ?(ЗначениеЗаполнено(Подразделение), " /" + Строка(Подразделение) + "/", "");
	
	Возврат НаименованиеПозиции;
	
КонецФункции

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	СписокСвязанныхРеквизитов = "Подразделение";
	УБ_ОбщегоНазначения.УстановитьТипЗначенияСвязанныхРеквизитовПоУмолчанию(Объект, СписокСвязанныхРеквизитов);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьЭлементовФормы(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ДатаЗакрытия",
		"Доступность",
		Форма.Объект.Закрыта);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"Закрыта",
		"Доступность",
		Форма.Объект.Утверждена);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ДатаУтверждения",
		"Доступность",
		Форма.Объект.Утверждена);
				
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"МодельПланирования",
		"Видимость",
		ЗначениеЗаполнено(Форма.МодельПланирования));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"СоздатьНовуюМодельПланирования",
		"Доступность",
		НЕ ЗначениеЗаполнено(Форма.МодельПланирования));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"СоздатьНовуюМодельПланирования",
		"Видимость",
		НЕ ЗначениеЗаполнено(Форма.МодельПланирования));
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораНовойМоделиПланирования()
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Владелец", Объект.Ссылка);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Справочник.УБ_МоделиПланированияЭффективности.Форма.ФормаЭлемента", ПараметрыФормы, ЭтотОбъект);
	
	//ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуСозданияНовогоГрейдаЗавершение", ЭтотОбъект);
	//
	//ОткрытьФорму("Справочник.УБ_Грейды.Форма.ФормаЭлемента", ПараметрыФормы, ЭтотОбъект,,,, ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьНовуюМодельПланирования(Команда)
	                                        
	Если Объект.Ссылка.Пустая() Или ЭтотОбъект.Модифицированность Тогда
		ЭтотОбъект.Записать();	
	КонецЕсли;
	
	ОткрытьФормуВыбораНовойМоделиПланирования();
КонецПроцедуры

#Область СтандартныеПодсистемы_ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры



#КонецОбласти

#КонецОбласти