
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УБ_ОбщегоНазначения.СкорректироватьТекстЗапросаПодТекущуюКонфигурацию(Список.ТекстЗапроса);
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("Организация") И ЗначениеЗаполнено(Параметры.Организация) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Организация", Параметры.Организация);
	КонецЕсли;
	
	Если Параметры.Свойство("Подразделение") И ЗначениеЗаполнено(Параметры.Подразделение) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Подразделение", Параметры.Подразделение);
	КонецЕсли;
	
	Если Параметры.Свойство("ДатаПримененияОтбора") И ЗначениеЗаполнено(Параметры.ДатаПримененияОтбора) Тогда
		ДатаПримененияОтбора = Параметры.ДатаПримененияОтбора;
		ПоказыватьЗакрытыеМодели = Истина;
		Элементы.ПоказыватьЗакрытыеМодели.Видимость = Ложь;
	Иначе
		ДатаПримененияОтбора = '00010101';
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ДатаПримененияОтбора", ДатаПримененияОтбора);
	
	УстановитьОтборСписка(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказыватьЗакрытыеМоделиПриИзменении(Элемент)
	
	УстановитьОтборСписка(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.МодельЗакрыта");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Список");
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.СкрытыйВариантОтчетаЦвет);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборСписка(Форма)
	
	Список = Форма.Список; 
	Если Не Форма.ПоказыватьЗакрытыеМодели Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "МодельЗакрыта", Ложь);
	Иначе
		ОбщегоНазначенияКлиентсервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "МодельЗакрыта");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСоздать(Команда)  
	ПараметрыФормы = Новый Структура;
	Отбор = Новый Структура;
	ПараметрыФормы.Вставить("ПоказыватьСвободные", Истина);
	Для каждого Элемент Из Список.Отбор.Элементы Цикл
		Отбор.Вставить(Строка(Элемент.ЛевоеЗначение), Элемент.ПравоеЗначение);
	КонецЦикла;
	ПараметрыФормы.Вставить("Отбор", Отбор);
	Форма = ОткрытьФорму("Справочник.УБ_ШтатноеРасписание.Форма.ФормаВыбора", ПараметрыФормы, Элементы.Список);
КонецПроцедуры

#КонецОбласти
