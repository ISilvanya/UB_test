#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированияОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированияОбъектов

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	
	Если Параметры.Свойство("СтрокаПоиска") И Не ПустаяСтрока(Параметры.СтрокаПоиска) Тогда
		СтрокаПоиска = Параметры.СтрокаПоиска;
	Иначе
		СтрокаПоиска = "";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 50
		|	МоделиПланированияЭффективности.Ссылка КАК Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(МоделиПланированияЭффективности.Ссылка) КАК Представление,
		|	МоделиПланированияЭффективности.Наименование КАК Наименование,
		|	МоделиПланированияЭффективности.Код КАК Код,
		|	МоделиПланированияЭффективности.ПометкаУдаления КАК ПометкаУдаления
		|ИЗ
		|	Справочник.УБ_МоделиПланированияЭффективности КАК МоделиПланированияЭффективности
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УБ_ШтатноеРасписание КАК РеквизитыШтатноеРасписание
		|		ПО МоделиПланированияЭффективности.ШтатноеРасписание = РеквизитыШтатноеРасписание.Ссылка
		|ГДЕ
		|	НЕ МоделиПланированияЭффективности.ЭтоГруппа
		|	И (&УсловияОтбораПоНаименованию ИЛИ &УсловияОтбораПоКоду)";
	
	Если Параметры.Свойство("Организация")
		И ЗначениеЗаполнено(Параметры.Организация) Тогда
		
		Запрос.Текст = Запрос.Текст + "
			|	И РеквизитыШтатноеРасписание.Подразделение.Владелец = &Организация";
		
		Запрос.УстановитьПараметр("Организация", Параметры.Организация);
		
	КонецЕсли;
	
	Если Параметры.Свойство("Подразделение")
		И ЗначениеЗаполнено(Параметры.Подразделение) Тогда
		
		Запрос.Текст = Запрос.Текст + "
			|	И РеквизитыШтатноеРасписание.Подразделение = &Подразделение";
		
		Запрос.УстановитьПараметр("Подразделение", Параметры.Подразделение);
		
	КонецЕсли;
	
	Если Параметры.Свойство("ДатаПримененияОтбора") Тогда
		
		Запрос.Текст = Запрос.Текст + "
			|	И (НЕ РеквизитыШтатноеРасписание.Закрыта ИЛИ &ДатаПримененияОтбора < РеквизитыШтатноеРасписание.ДатаЗакрытия)";
		
		Запрос.УстановитьПараметр("ДатаПримененияОтбора", Параметры.ДатаПримененияОтбора);
		
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	МоделиПланированияЭффективности.Наименование";
	
	УБ_ОбщегоНазначения.СкорректироватьТекстЗапросаПодТекущуюКонфигурацию(Запрос.Текст);
	
	Если Не ПустаяСтрока(СтрокаПоиска) Тогда
		
		Запрос.УстановитьПараметр("СтрокаПоиска", СтрокаПоиска + "%");
		Запрос.УстановитьПараметр("СтрокаПоискаПоКоду", "%" + СтрокаПоиска + "%");
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&УсловияОтбораПоНаименованию",
			"МоделиПланированияЭффективности.Наименование ПОДОБНО &СтрокаПоиска");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&УсловияОтбораПоКоду",
			"МоделиПланированияЭффективности.Код ПОДОБНО &СтрокаПоискаПоКоду");
		
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловияОтбораПоНаименованию", "ИСТИНА");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловияОтбораПоКоду", "ИСТИНА");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДлинаСтрокиПоиска = СтрДлина(СтрокаПоиска);
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ПустаяСтрока(СтрокаПоиска) И Не СтрНачинаетсяС(ВРег(Выборка.Представление), ВРег(СтрокаПоиска)) Тогда
			
			ПредставлениеМодели = "(" + Выборка.Код + ") " + Выборка.Представление;
			
			ПозицияСтрокиПоиска = СтрНайти(ВРег(ПредставлениеМодели), ВРег(СтрокаПоиска));
			Представление = Новый ФорматированнаяСтрока(
				Лев(ПредставлениеМодели, ПозицияСтрокиПоиска - 1),
				Новый ФорматированнаяСтрока(
					Сред(ПредставлениеМодели, ПозицияСтрокиПоиска, ДлинаСтрокиПоиска),
					Новый Шрифт( , , Истина),
					WebЦвета.Зеленый),
				Сред(ПредставлениеМодели, ПозицияСтрокиПоиска + ДлинаСтрокиПоиска));
			
		Иначе
			
			ПредставлениеМодели = Выборка.Представление + " (" + Выборка.Код + ")";
			
			Представление = Новый ФорматированнаяСтрока(
				Новый ФорматированнаяСтрока(
					Лев(ПредставлениеМодели, ДлинаСтрокиПоиска),
					Новый Шрифт( , , Истина),
					WebЦвета.Зеленый),
				Сред(ПредставлениеМодели, ДлинаСтрокиПоиска + 1));
			
		КонецЕсли;
		
		ЗначениеВыбора = Новый Структура;
		ЗначениеВыбора.Вставить("Значение", Выборка.Ссылка);
		ЗначениеВыбора.Вставить("ПометкаУдаления", Выборка.ПометкаУдаления);
		ЗначениеВыбора.Вставить("Предупреждение", "");
		
		ДанныеВыбора.Добавить(ЗначениеВыбора, Представление, Выборка.ПометкаУдаления);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли